label tr_001:
    $ _window = False
    scene bg black with dissolve
    pause 0.3
    show text "Chapter 1:\n{size=+10}{b}A Certain Scientific Subject{/b}{/size}{fast}"
    $ narrator = regnar
    pause
    hide text with dissolve
    
    scene bg hall 1 with fade
    $ _window = True
    play music "music/normal_theme.ogg"
    show latimer lab serious 1 at left
    show pimentel summer happy 1 at right
    pc "Uh... Run it by me again. How is this going to work?"
    p "It's basically pattern matching. People are creatures of habit. All we're doing is interpolating the future by identifying these patterns and extending them."
    pc "Uh huh... And how exactly are you guys doing that?"
    
    "*shuffle* *shuffle*"
    #Shuffling of paper. 
    
    show latimer lab serious 1 at center with move
    hide pimentel with dissolve
    l "If you look through these folders, you can see how we will do it."
    
    "Latimer passes me a bunch of folders. I open one to see what looks like detailed reports. As I inspect them closer,
    I realize that these contain descriptions about someone's daily activities."
    
    pc " 'Subject took the market road home today. He looked so delightful~ Oh,then he stopped to buy dinner at Wagnaria. It was Combo D, with mashed 
    poatoes and an orange soda. It made me so hungry just looking at it. I want to eat him up~ {image=effects/heart_white.png}. When he finally got home, he took a quick nap before turning on the TV and watching-"
    pc "Wait a second, I did that yesterday. Is this {i}me?{/i}"
    
    l "It would appear so. Most of our reports are not as informal."
    show latimer lab serious 1 at left with move
    show pimentel summer lovestruck 1 at right with dissolve
    p "Kyaa~ Whenever I write your report, I can't stop myself. You're too adorable my darling~"
    
    "Geh. She just jumped on my back."
    
    pc "Get... off.. me..."
    p "Don't want."
    p "Ehehe~ {image=effects/heart_white.png}"
    
    "Ah whatever. That's not really the problem right now."
    
    pc "You guys have been following me? Knock it off!"
    hide latimer with dissolve
    show pimentel summer dramatic 1 at center with move
    p "Boo~ That's no fun."
    pc "Am I really that entertaining to stalk?"
    show pimentel summer happy 1 at center with dissolve
    "She hugs me tighter in response to my question."
    
    p "You make everyday so much more interesting~ {image=effects/heart_white.png}"
    
    "Can't... breathe..."
    
    pc "Are all these folders about my day to day activities? How long have you guys been doing this?"
    p "Oh they're not all about you my darling. That batch of folders is on our class."
    
    pc "You're stalking everyone in class too? Pimentel-"
    show pimentel summer stern 1 at center with dissolve
    p "Boo~ Call me Yurika my darling."
    pc "What? We've been this already. I'm not that comfortable with calling you by your first name yet. It's only been-"
    
    "I'm interrupted by something wet and warm near my ear. Pimentel is suddenly breathing into it."
    
    p "You know we're close right?"
    
    "And then she nibbles on it."
    
    with vpunch
    
    pc "What the hell Pimentel?!"
    p "Yurika."
    pc "Okay, okay, Y-yu-"
    
    "Oh come on, this isn't hard."
    
    pc "Yu-Yurika!"
    
    "There I said it."
    hide pimentel with dissolve
    pause 1.5
    "The next second, I am defeaned by something really loud exploding right by me ear."
    
    show pimentel summer lovestruck 1 at center with dissolve
    p "Wowwww~ You actually said my name! I'm so happy~ {image=effects/heart_white.png}"
    
    "She proceedes to cuddle against my back. This is starting to get a little bit too much."
    
    "Also, where was I? Too much distractions."
    
    pc "Wait wait wait, like I was saying, you guys, are you stalking everyone in our class too?"
    show pimentel summer happy 1 at right with move
    show latimer lab serious 1 at left with dissolve
    l "This is not stalking. We are conducting observational research."
    "Isn't that the same thing?"
    l "Our sample is also a lot bigger than just the class."
    pc "Wait, what? How much bigger?"
    l "Roughly 333 times more."
    pc "Uh... 333... Carry the 1..."
    
    hide pimentel with dissolve
    show latimer lab serious 1 at center with move
    l "That is about the size of the town's population."
    
    "Come again? Did she just say they were stalking the entire town?"
    
    pc "Uh... I think I misheard. Did you really say the entire town?"
    l "Yes."
    pc "Huh."
    
    #wait 2 seconds?
    
    pause 2.0
    
    pc "Isn't.... Isn't that illegal?"
    l "There are no laws against it."
    pc "Uh... What aboout trespassing?"
    l "Only events that occur in public space are recorded."
    
    "I look at the papers in my hand. These definitely don't seem very public to me. I read off one of the entries again."
    
    pc "'After finishing his pasta, my darling went to straight to bed. Goodnight~ {image=effects/heart_white.png}'"
    pc "That's hardly public space Latimer."
    show latimer lab serious 1 at left with move
    show pimentel summer happy 1 at right with dissolve
    p "Ehehe~"
    l "Yuriko gets carried away when she writes hers."
    p "I can't help it. You're a special case."
    l "To be fair, yours is the only report that actually has this level of detail. If you look at this one, you can see that this is strictly professional."
    
    "She hands me another folder. Opening it, I immediately see the difference between this and mine."
    hide pimentel with dissolve
    show latimer lab serious 1 at center with move
    "'Subject heads for 1254 Spruce street. Subject runs into Subject 33522 and talks with her for 3 minutes and 23 seconds. Subject continues journey after saying goodbye.'"
    
    pc "Hmm. I don't know, I'm still not exactly sold on this idea. And how do you guys even have the time to do this?"
    l "It is all automated."
    pc "What, robots?"
    l "Sure."
    pc "That doesn't really answer the question Latimer."
    show latimer lab serious 1 at left with move
    show pimentel summer happy 1 at  right with dissolve
    p "I'll tell you if you give me a kiss my darling~"
    pc "Knock it off!"
    "I try to push her away but she steals a quick kiss anyway."
    p "Aww, you're blushing. That's adorable~ {image=effects/heart_white.png}"
    hide latimer with dissolve
    show pimentel summer lovestruck 1 at center with move
    pc "S-shut up!"
    pc "A-anyway, look, don't you think there's something wrong with what you're doing?"
    
    show pimentel summer happy 1 at right with move
    show latimer lab serious 1 at left with dissolve
    l "No. We're merely observing people."
    pc "But you're following them around and stuff."
    l "The only individual we follow around is you Subject Alpha, and that is only because Yurika insists on doing it."
    p "Ehehe~ {image=effects/heart_white.png}"
    pc "What about these other reports?"
    l "We use the traffic cameras."
    pc "Don't those only activate when they see a car or speeding or something?"
    l "We, uhm, modified them a bit."
    pc "So what you're doing {i}is{/i} illegal then."
    l "Oh no no, they still maintain their original functionality. Besides, we already got permission for this."
    pc "The police is fine with you stalking people?"
    l "They had no objections with our original request."
    pc "You {i}asked{/i} them if you could stalk people with their equipment?"
    l "Not exactly."
    hide pimentel with dissolve
    show latimer lab annoyed 1 at center with move
    
    l "I do not understand your fixation on stalking Subject Alpha."

    "IT'S NOT ME WHO'S DOING IT! AHHHHHHHHHHHHHHHH."
    
    menu:
        "Gah, I give up! Let them do whatever the hell they want.":
            jump continue_1
        "Gah, I give up! I'm going to use Evans' idea instead.":
            pass

    pc "Look, I have no idea what you guys are doing and I don't want to get into trouble. I'm going to see Evans about her idea instead."
    l "But-"
    pc "No buts. This is final."
    
    show latimer lab annoyed 1 at left with move
    show pimentel summer dramatic 1 at right with dissolve
    pause 1.0
    
    scene bg black with fade
    
    jump tw_sproul_end
    #"And you did go and do it. What a fool. Do you even know how VNs work man?"
    #jump twins_end
    #fade to black
    #END
    
    #The RIGHT Choice:
    label continue_1:
        "I sigh."
        "I give up. Whatever. This isn't going anywhere. They know what they're doing."
        
        pc "Ah, forget it. Do whatever you want Latimer."
        show latimer lab happy 1 at center with dissolve
        l "Thank you Subject Alpha."
        l "Oh, and Subject Alpha?"
        pc "What?"
        l "You can call me Mikoto."
        show latimer lab glad 1 at center with dissolve
        
        "*splutter* What the hell. First Pimentel, now Latimer. What is it with these twins?"
        show latimer lab glad 1 at left with move
        show pimentel summer stern 1 at right with dissolve
        p "Ehhhhhhhh? No fair~ I'm the only one my darling should be on first name basis with."
        l "That hardly seems fair."
        p "But he's MINE."
        
        "Geh, she just tightened her arms around me even more."
        show pimentel summer happy 1 at right with dissolve
        pc "Look, ah, Latimer-"
        show latimer lab serious 1 at left with dissolve
        l "Mikoto."
        pc "Gah, Lati-"
        l "Mikoto."
        pc "La-"
        l "Mikoto."
        
        hide pimentel with dissolve
        show latimer lab serious 1 at center with move
        "I stare at her in exasperation. She hardly flinches though and holds my gaze."
        
        "If this is how it's going down, then I'm not going to lose!"
        
        "*Staaaaaarrrre*"
        "*Staaaaaaaaaarrrrrrrrre*"
        "*Staaaaaaaaaaaaaaaaarrrrrrrrrrrrre*"
        "*Staaaaaaaaaaaaaaaaaaaaaaaaarrrrrrrrrrrrrrrre*"
        
        menu:
            "*Staaaaaaaaaaaaaaaaaaaaaaaaaaarrrrrrrrrrrrrrrrrre*":
                jump continue_2
            "Kick her in the face":
                pass

        "I ready myself for the kick before I realize how stupid it would be to actually do it."
        "Come on, concentrate man."
        #continue to good choice
        
        #Good choice
label continue_2:
    "*Staaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaaarrrrrrrrrrrrrrrrrrrrrrrrrre*"
    "HaHA! I'm winning!"
    with vpunch
    "*Poke*"
    with hpunch
    "GAH, MY EYES."
    
    "I crouch down in pain, dislodging Pimentel in the process."
    p "Mukyu!"
    pc "What the HELL Latimer."
    show latimer lab glad 1 at center with dissolve
    l "I win. You have to call me Mikoto now."
    pc "You cheated!"
    l "There were no rules to begin with."
    
    "It's no use. There's no arguing with this girl."
    
    "I sigh."
    pc "Alright, alright. Mikoto."
    
    show latimer lab happy 1 at center with dissolve
    "She flashes a brief smile before giving me a nod."
    l "Very good."
    show latimer lab happy 1 at left with move
    show pimentel summer stern 1 at right with dissolve
    p "Hmph."
    l "Now, let me look at your eyes."
    "I quickly back away."
    pc "Whoa, I don't want any more damage on them."

    l "No, hold still. Let me see."
    hide pimentel with dissolve
    show latimer lab serious 1 at center with move
    "She grabs my head with both her hands and stares at my eyes."
    "Ah, this is kinda embarassing."
    "After a minute of inspection, she lets go and nods."
    l "No permanent damage."
    pc "Uh, how about immediate damage?"
    "She chooses not to answer. Instead, she leans closer to me in a way that has her obstructing Pimentel's view with her back."
    l "I don't think it's fair that Yurika's the only one who gets a kiss."
    show latimer lab happy 1:
        ease 0.6 zoom 5.0 ypos 3.05
    "And she kisses me on the forehead."
    pause 2.0
    
    #Some sort of wait?
    
    show latimer lab stern 1 at center:
        ease 1.0 zoom 1.0 ypos 1.0
    with dissolve
    "I'm too surprised to do anything, and by the looks of Latimer's face, it looks like she's completely taken aback by what she did too.
    Blushing furiously, she backs away and mumbles something about an experiment before grabbing Pimentel by the hand and running off."
    
    show latimer lab stern 1 at left with move
    show pimentel summer worried 1 at right with dissolve
    p "Hey, what's the rush Mikoto? Ah, don't pull so hard! Bye my darling! Have a nice summer~ {image=effects/heart_white.png}"
    
    show latimer lab stern 1 at center with move
    pause 0.2
    hide latimer
    hide pimentel
    with moveoutleft
    "I sit on the floor, still speechless."
    "I'm not exactly sure what just happened today."
    
    #sound of footsteps
    show latimer lab stern 1 at left with MoveInLeft(1.0)
    "I look up and I see Latimer."
    pc "Ah, I-"
    "She thrusts a piece of paper into my hand and runs off again before I can say another word."
    hide latimer with moveoutleft
    
    "She's gone by the time I regain my senses. Looking at the piece of paper in my hand, I proceed to unfold it and see that 
    there's writing inside. Kinda expected it actually."
    
    "It reads:"
    "' Subject Alpha: Project tomorrow. STOP. Come to my house. STOP. Bring sports shoes.'"
    
    "I chuckle. There's the Latimer I've grown used to."
    "I pick myself up from the floor and dust my pants."
    pc "Time to go home I guess."
    
    jump tw_end
        
label tw_end:
    scene bg black with fade
    return
    
label tw_sproul_end:
    "Ding Dong Ding Dong....it's time for \"Teach me, Sproul-Sensei!\""
    scene bg classroom 2 with fade
    show sproul sensei worried 1 at center with dissolve
    ss "Arara... Did you forget that you were playing a Visual Novel?"
    show sproul sensei happy 1 at center with dissolve
    ss "Remember, you're in the twins' route so you should concentrate on them."
    show sproul sensei coy 1 at center with dissolve
    ss "Unless..."
    ss "Did you want Evans' route? Hmm... It seems a bit too late now."
    show sproul sensei happy 1 at center with dissolve
    ss "Oh well. I'll see you next time if you mess up again."
    ss "Good luck~"
    scene bg black with fade
    jump demo_end
